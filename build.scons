Import('*')

sources = Split('''
    animation.cpp
    animation_action.cpp
    animation_cycle.cpp
    bone.cpp
    buffersource.cpp
    cal3d_wrapper.cpp
    calxmlbindings.cpp
    coreanimatedmorph.cpp
    coreanimation.cpp
    corebone.cpp
    corekeyframe.cpp
    corematerial.cpp
    coremesh.cpp
    coremodel.cpp
    coremorphkeyframe.cpp
    coremorphtrack.cpp
    coreskeleton.cpp
    coresubmesh.cpp
    coresubmorphtarget.cpp
    coretrack.cpp
    error.cpp
    global.cpp
    loader.cpp
    matrix.cpp
    mesh.cpp
    mixer.cpp
    model.cpp
    physique.cpp
    platform.cpp
    quaternion.cpp
    renderer.cpp
    saver.cpp
    skeleton.cpp
    streamsource.cpp
    submesh.cpp
    tinybind.cpp
    tinystr.cpp
    tinyxml.cpp
    tinyxmlerror.cpp
    tinyxmlparser.cpp
    vector.cpp
    xmlformat.cpp
''')

exporter_sources = Split('''
    MaxAnimationExport.cpp
    MaxAnimationExportDesc.cpp
    MaxAnimationExportMaxscriptCall.cpp
    MaxInterface.cpp
    MaxMaterial.cpp
    MaxMaterialExport.cpp
    MaxMaterialExportDesc.cpp
    MaxMaterialExportMaxscriptCall.cpp
    MaxMesh.cpp
    MaxMeshExport.cpp
    MaxMeshExportDesc.cpp
    MaxMeshExportMaxscriptCall.cpp
    MaxMorphAnimationExport.cpp
    MaxMorphAnimationExportDesc.cpp
    MaxMorphAnimationExportMaxscriptCall.cpp
    MaxNode.cpp
    MaxNullView.cpp
    MaxSkeletonExport.cpp
    MaxSkeletonExportDesc.cpp
    MaxSkeletonExportMaxscriptCall.cpp
    StdAfx.cpp
    cal3d_max_exporter.cpp
    cal3d_max_exporter.def
''')

exporter_src_sources = Split('''
    BaseInterface.cpp
    BaseMaterial.cpp
    BaseMesh.cpp
    BaseNode.cpp
    BoneCandidate.cpp
    Exporter.cpp
    Lodder.cpp
    MaterialCandidate.cpp
    MaterialLibraryCandidate.cpp
    MeshCandidate.cpp
    MorphAnimationCandidate.cpp
    Quadric.cpp
    SkeletonCandidate.cpp
    SubmeshCandidate.cpp
    VertexCandidate.cpp
''')
exporter_win32_sources = Split('''
    AnimationExportSheet.cpp
    AnimationTimePage.cpp
    BoneAssignmentPage.cpp
    LevelOfDetailPage.cpp
    MaterialExportSheet.cpp
    MaterialMapDialog.cpp
    MaterialMapsPage.cpp
    MaterialSelectionPage.cpp
    MeshExportSheet.cpp
    MorphAnimationExportSheet.cpp
    MorphAnimationTimePage.cpp
    SkeletonExportSheet.cpp
    SkeletonFilePage.cpp
    SkeletonHierarchyPage.cpp
    SpringSystemPage.cpp
''')

#   SelectionDialog.cpp

# tinytest.cpp

sources = ['src/cal3d/' + s for s in sources]
exporter_sources = ['plugins/cal3d_max_exporter/' + s for s in exporter_sources]
exporter_src_sources = ['plugins/src/' + s for s in exporter_src_sources]
exporter_win32_sources = ['plugins/src/win32/' + s for s in exporter_win32_sources]

shared_lib_env = env.Copy(tools=[boost])
shared_lib_env.Append(
    CPPDEFINES=['CAL3D_EXPORTS'],
    CPPPATH=['../src'])

PCH_OBJ = imvu_enable_pch(shared_lib_env, 'src/cal3d/PCH.cpp', 'PCH.h')

imvu_shared_library(
    shared_lib_env,
    'cal3d${IMVU_LIBRARY_SUFFIX}.dll',
    sources + [PCH_OBJ])

# Build tests.
if not ARGUMENTS.get('skiptests', ''):
    SConscript('test/shared.scons')

exporter_env = env.Copy(tools=[cal3d, boost])
exporter_env.Append(
    CPPPATH=['../plugins/cal3d_max_exporter',
             '#/Source/cal3d/plugins/src',
             '#/Source/cal3d/plugins/src/win32',
             '#/ext/3dsmax7/maxsdk/include',
             '#/Ext/3dsmax7/maxsdk/cssdk/include',
             ],
    CPPDEFINES=['_AFXDLL'],
    LIBPATH=['#/Ext/3dsmax7/maxsdk/lib'],
    LIBS=['core',
          'geom',
          'gfx',
          'mesh',
          'maxutil', 
          'maxscrpt', 
          'paramblk2'])

exporter_env['CPPDEFINES'].remove('NOMINMAX')
exporter_env['CPPDEFINES'].remove('UNICODE')
exporter_env['CPPDEFINES'].remove('_UNICODE')

PCH_OBJ = imvu_enable_pch(exporter_env, 'plugins/src/PCH.cpp', 'PCH.h')

res = exporter_env.RES('plugins/cal3d_max_exporter/cal3d_max_exporter.rc')
exporter_env.SharedLibrary(
    '../bin/${IMVU_BUILDDIR_NAME}/cal3d_max_exporter',
    exporter_sources + exporter_src_sources + exporter_win32_sources + [res, PCH_OBJ])
exporter_env.InstallAs(
    '#/Binaries/${IMVU_BUILDDIR_NAME}/cal3d_max_exporter.dle',
    '../bin/${IMVU_BUILDDIR_NAME}/cal3d_max_exporter.dll')
